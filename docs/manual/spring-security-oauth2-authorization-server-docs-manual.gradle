apply plugin: 'io.spring.convention.docs'
apply plugin: 'io.spring.convention.springdependencymangement'
apply plugin: 'io.spring.convention.dependency-set'
apply plugin: 'io.spring.convention.repository'
apply plugin: 'java'

asciidoctor {
	attributes([stylesheet: 'css/style.css'])
	resources {
		from(sourceDir) {
			include "css/**"
		}
	}
}

asciidoctorj {
	def ghTag = snapshotBuild ? 'main' : project.version
	def ghUrl = "https://github.com/spring-projects-experimental/spring-authorization-server/tree/$ghTag"
	attributes 'spring-authorization-server-version' : project.version,
		'spring-boot-version' : springBootVersion,
		revnumber : project.version,
		'gh-url': ghUrl,
		'gh-samples-url': "$ghUrl/samples"
	attributeProvider resolvedVersions(project.configurations.testCompile)
}

def resolvedVersions(Configuration configuration) {
	return {
		configuration.resolvedConfiguration
			.resolvedArtifacts
			.collectEntries { [(it.name + "-version"): it.moduleVersion.id.version] }
	}
}

repositories {
	maven { url "https://repo.spring.io/release" }
}
